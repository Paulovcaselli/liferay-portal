@component-name = "portal-user-management-and-portal-configuration"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Password Policies";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "test@liferay.com",
				userLoginFullName = "Test Test");

			JSONUser.tearDownNonAdminUsers();

			PasswordPolicies.tearDownCP();
		}
	}

	@description = "This is a use case for LPS-60180. This test enables Password Syntax Checking. It also asserts that the specific error message displays when user attempts to enter a short password."
	@priority = "4"
	test ErrorMessageDisplaysWhenPasswordIsShorterThanMinimumLength {
		property custom.properties = "mail.send.blacklist=noreply@liferay.com,noreply@domain.invalid,test@domain.invalid";
		property test.smtp.server.enabled = "true";

		var portalURL = PropsUtil.get("portal.url");

		PortalSettings.gotoConfiguration(
			configurationCategory = "Email",
			configurationName = "Email Sender",
			configurationScope = "Virtual Instance Scope");

		PortalSettings.configureEmailSender(
			emailAddress = "test@liferay.com",
			emailSenderName = "Joe Bloggs");

		ApplicationsMenu.gotoPortlet(
			category = "Security",
			panel = "Control Panel",
			portlet = "Password Policies");

		// Enabling Syntax Checking will set minimum length to 6 by default

		PasswordPolicies.enableSyntaxChecking(passwordPolicyName = "Default Password Policy");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.addCP(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		User.logoutPG();

		User.forgotPasswordSendResetLink(userEmailAddress = "userea@liferay.com");

		MockMock.openBodyLink(
			asset = "${portalURL}: Reset Your Password",
			content = "You can reset your password for ${portalURL}",
			subject = "${portalURL}: Reset Your Password");

		Type(
			locator1 = "UsersAndOrganizationsEditUser#PASSWORD_NEW_PASSWORD_FIELD",
			value1 = "test");

		Type(
			locator1 = "UsersAndOrganizationsEditUser#PASSWORD_ENTER_AGAIN_FIELD",
			value1 = "test");

		Button.clickSave();

		AssertTextEquals.assertPartialText(
			locator1 = "Message#ERROR",
			value1 = "That password is too short. Please make sure your password is at least 6 characters long.");

		// Asserts user's password did not change

		User.loginPG(
			password = "password",
			resetPassword = "password2",
			syntaxCheckingEnabled = "true",
			userEmailAddress = "userea@liferay.com");
	}

	@description = "This is a use case for LPS-118342"
	@priority = "3"
	test LoginUserAfterPasswordPolicyChanged {
		ApplicationsMenu.gotoPortlet(
			category = "Security",
			panel = "Control Panel",
			portlet = "Password Policies");

		PasswordPolicies.enableChangeable(
			passwordPolicyName = "Default Password Policy",
			passwordPolicyPanel = "Password Changes");

		ApplicationsMenu.gotoPortlet(
			category = "Security",
			panel = "Control Panel",
			portlet = "Password Policies");

		PasswordPolicies.enableChangeable(
			enableChangeRequired = "false",
			passwordPolicyName = "Default Password Policy",
			passwordPolicyPanel = "Password Changes");

		ApplicationsMenu.gotoPortlet(
			category = "Security",
			panel = "Control Panel",
			portlet = "Password Policies");

		PasswordPolicies.togglePasswordExpiration(
			enablePasswordExpiration = "true",
			graceLimit = "0",
			minimumAge = "52 Weeks",
			passwordPolicyName = "Default Password Policy",
			warningTime = "2 Weeks");

		User.logoutPG();

		User.createAccount(
			newPassword = "Password1",
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		User.loginPG(
			password = "Password1",
			userEmailAddress = "userea@liferay.com");
	}

	@description = "This test sets the Password Changes Minimum Age to 30 minutes to assert the error message when a user attempts to change password right away."
	@priority = "4"
	test UserIsNotAbleToChangePasswordWithinThePasswordMinimumAge {
		JSONUser.addUser(
			userEmailAddress = "userea2@liferay.com",
			userFirstName = "userfn2",
			userLastName = "userln2",
			userScreenName = "usersn2");

		ApplicationsMenu.gotoPortlet(
			category = "Security",
			panel = "Control Panel",
			portlet = "Password Policies");

		PasswordPolicies.enableChangeable(
			enableChangeRequired = "false",
			minimumAge = "30 Minutes",
			passwordPolicyName = "Default Password Policy",
			passwordPolicyPanel = "Password Changes");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		UserNavigator.gotoUser(userScreenName = "usersn2");

		UserNavigator.gotoPassword();

		Type(
			key_text = "New Password",
			locator1 = "TextInput#ANY",
			value1 = "pass");

		Type(
			key_text = "Enter Again",
			locator1 = "TextInput#ANY",
			value1 = "pass");

		Button.clickSave();

		Alert.viewSuccessMessage();

		Type(
			key_text = "New Password",
			locator1 = "TextInput#ANY",
			value1 = "test");

		Type(
			key_text = "Enter Again",
			locator1 = "TextInput#ANY",
			value1 = "test");

		Button.clickSave();

		Alert.viewRequestFailedToComplete();

		AssertTextEquals.assertPartialText(
			locator1 = "Message#ERROR_1",
			value1 = "You cannot change your password yet. Please wait");
	}

	@description = "This test turns on Enable Password History. It attempts to reuse a password and asserts that a user cannot do so."
	@priority = "4"
	test UserIsNotAbleToReuseAPasswordIfHistoryIsEnabled {
		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		ApplicationsMenu.gotoPortlet(
			category = "Security",
			panel = "Control Panel",
			portlet = "Password Policies");

		PasswordPolicies.enableHistory(
			historyCount = "2",
			passwordPolicyName = "Default Password Policy",
			passwordPolicyPanel = "Password History");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		UserNavigator.gotoUser(userScreenName = "usersn");

		UserNavigator.gotoPassword();

		// First password

		Type(
			key_text = "New Password",
			locator1 = "TextInput#ANY",
			value1 = "pass");

		Type(
			key_text = "Enter Again",
			locator1 = "TextInput#ANY",
			value1 = "pass");

		Button.clickSave();

		Alert.viewSuccessMessage();

		// Second password

		Type(
			key_text = "New Password",
			locator1 = "TextInput#ANY",
			value1 = "password");

		Type(
			key_text = "Enter Again",
			locator1 = "TextInput#ANY",
			value1 = "password");

		Button.clickSave();

		Alert.viewSuccessMessage();

		// Attempt to input first password

		Type(
			key_text = "New Password",
			locator1 = "TextInput#ANY",
			value1 = "pass");

		Type(
			key_text = "Enter Again",
			locator1 = "TextInput#ANY",
			value1 = "pass");

		Button.clickSave();

		Alert.viewRequestFailedToComplete();

		AssertTextEquals.assertPartialText(
			locator1 = "Message#ERROR_1",
			value1 = "That password has already been used. Please enter a different password.");

		// Enter a different password so we can reach history count of 2

		Type(
			key_text = "New Password",
			locator1 = "TextInput#ANY",
			value1 = "pass2");

		Type(
			key_text = "Enter Again",
			locator1 = "TextInput#ANY",
			value1 = "pass2");

		Button.clickSave();

		Alert.viewSuccessMessage();

		// Assert entering first password works after reaching history count

		Type(
			key_text = "New Password",
			locator1 = "TextInput#ANY",
			value1 = "pass");

		Type(
			key_text = "Enter Again",
			locator1 = "TextInput#ANY",
			value1 = "pass");

		Button.clickSave();

		Alert.viewSuccessMessage();

		// Disabling Password History

		ApplicationsMenu.gotoPortlet(
			category = "Security",
			panel = "Control Panel",
			portlet = "Password Policies");

		PasswordPolicies.disableHistory(
			passwordPolicyName = "Default Password Policy",
			passwordPolicyPanel = "Password History");

		// After disabling history, we will assert that password history doesn't save

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		UserNavigator.gotoUser(userScreenName = "usersn");

		UserNavigator.gotoPassword();

		for (var password : list "pass3,password") {
			Type(
				key_text = "New Password",
				locator1 = "TextInput#ANY",
				value1 = "${password}");

			Type(
				key_text = "Enter Again",
				locator1 = "TextInput#ANY",
				value1 = "${password}");

			Button.clickSave();

			Alert.viewSuccessMessage();
		}
	}

	@priority = "4"
	test ViewInvalidPasswordChangeSyntax {
		ApplicationsMenu.gotoPortlet(
			category = "Security",
			panel = "Control Panel",
			portlet = "Password Policies");

		PasswordPolicies.enableSyntaxChecking(passwordPolicyName = "Default Password Policy");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.addCP(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.editPasswordInvalidCP(
			minimumNumbers = "1",
			newPassword = "password",
			userScreenName = "usersn");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.editPasswordInvalidCP(
			newPassword = "pass",
			passwordTooShort = "true",
			userScreenName = "usersn");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		UserNavigator.gotoUser(userScreenName = "usersn");

		UserNavigator.gotoPassword();

		Type(
			locator1 = "UsersAndOrganizationsEditUser#PASSWORD_NEW_PASSWORD_FIELD",
			value1 = "Password1");

		Type(
			locator1 = "UsersAndOrganizationsEditUser#PASSWORD_ENTER_AGAIN_FIELD",
			value1 = "Password1");

		PortletEntry.save();

		User.logoutPG();

		Navigator.openURL();

		AssertClick(
			locator1 = "UserBar#USER_SIGN_IN",
			value1 = "Sign In");

		Type(
			locator1 = "TextInput#EMAIL_ADDRESS",
			value1 = "userea@liferay.com");

		Type(
			locator1 = "TextInput#PASSWORD",
			value1 = "Password1");

		Click(
			locator1 = "Button#SIGN_IN",
			value1 = "Sign In");

		AssertElementNotPresent(locator1 = "Button#SIGN_IN");

		User.acceptEndUserLicenseAgreement();

		Type(
			locator1 = "TextInput#PASSWORD",
			value1 = "password");

		Type(
			locator1 = "TextInput#ENTER_AGAIN",
			value1 = "password");

		Button.clickSave();

		AssertTextEquals.assertPartialText(
			locator1 = "Message#ERROR",
			value1 = "That password must contain at least 1 numbers.");

		Type(
			locator1 = "TextInput#PASSWORD",
			value1 = "pass");

		Type(
			locator1 = "TextInput#ENTER_AGAIN",
			value1 = "pass");

		Button.clickSave();

		AssertTextEquals.assertPartialText(
			locator1 = "Message#ERROR",
			value1 = "That password is too short. Please make sure your password is at least 6 characters long.");

		Type(
			locator1 = "TextInput#PASSWORD",
			value1 = "Password2");

		Type(
			locator1 = "TextInput#ENTER_AGAIN",
			value1 = "Password2");

		Button.clickSave();

		User.answerPasswordReminder();

		AssertElementPresent(locator1 = "UserBar#USER_AVATAR_IMAGE");

		User.logoutPG();

		User.loginUserPG();
	}

	@description = "This is a use case for LPS-119650"
	@priority = "3"
	test ViewInvalidRegularExpressionMessage {
		property custom.properties = "jsonws.web.service.paths.excludes=";

		var regex = '''^((?=.*?[a-z])(?=.*?\d)(?=.*?[;~!@ #$%^&*_\-+=`|\\(){}\[\]:;\"'<>,.?/])|(?=.*?[A-Z])(?=.*?\d)(?=.*?[;~!@ #$%^&*_\-+=`|\\(){}\[\]:;\"'<>,.?/])|(?=.*?[A-Z])(?=.*?[a-z])(?=.*?[;~!@ #$%^&*_\-+=`|\\(){}\[\]:;\"'<>,.?/])|(?=.*?[A-Z])(?=.*?[a-z])(?=.*?\d)).{12,24}$''';

		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		JSONUser.setFirstPassword(
			agreeToTermsAndAnswerReminderQuery = "true",
			requireReset = "false",
			userEmailAddress = "userea@liferay.com");

		ApplicationsMenu.gotoPortlet(
			category = "Security",
			panel = "Control Panel",
			portlet = "Password Policies");

		PasswordPolicies.enableSyntaxChecking(passwordPolicyName = "Default Password Policy");

		ApplicationsMenu.gotoPortlet(
			category = "Security",
			panel = "Control Panel",
			portlet = "Password Policies");

		PasswordPolicies.editSyntaxChecking(
			passwordPolicyName = "Default Password Policy",
			regularExpression = "${regex}");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.editPasswordInvalidCP(
			newPassword = "Habanero0*#",
			regex = "${regex}",
			userEmailAddress = "test1@liferay.com",
			userScreenName = "usersn");

		ApplicationsMenu.gotoPortlet(
			category = "Security",
			panel = "Control Panel",
			portlet = "Password Policies");

		PasswordPolicies.editSyntaxChecking(
			passwordPolicyName = "Default Password Policy",
			regularExpression = "(?=.{4})(?:[a-zA-Z0-9]*)");
	}

	@priority = "4"
	test ViewPasswordChangeableAndChangeRequired {
		property portal.acceptance = "true";

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.addCP(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.gotoEditCP(userScreenName = "usersn");

		UserNavigator.gotoPassword();

		AssertElementPresent(
			key_checkboxName = "Require Password Reset",
			locator1 = "Checkbox#CHECKBOX_DISABLED");

		ApplicationsMenu.gotoPortlet(
			category = "Security",
			panel = "Control Panel",
			portlet = "Password Policies");

		PasswordPolicies.enableChangeable(
			enableChangeRequired = "false",
			passwordPolicyName = "Default Password Policy",
			passwordPolicyPanel = "Password Changes");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.gotoEditCP(userScreenName = "usersn");

		UserNavigator.gotoPassword();

		AssertElementNotPresent(
			key_checkboxName = "Require Password Reset",
			locator1 = "Checkbox#CHECKBOX_DISABLED");

		ApplicationsMenu.gotoPortlet(
			category = "Security",
			panel = "Control Panel",
			portlet = "Password Policies");

		PasswordPolicies.enableChangeable(
			enableChangeRequired = "true",
			passwordPolicyName = "Default Password Policy",
			passwordPolicyPanel = "Password Changes");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.gotoEditCP(userScreenName = "usersn");

		UserNavigator.gotoPassword();

		AssertElementPresent(
			key_checkboxName = "Require Password Reset",
			locator1 = "Checkbox#CHECKBOX_DISABLED");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.editPasswordCP(
			userEmailAddress = "userea@liferay.com",
			userScreenName = "usersn");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.gotoEditCP(userScreenName = "usersn");

		UserNavigator.gotoPassword();

		AssertElementNotPresent(
			key_checkboxName = "Require Password Reset",
			locator1 = "Checkbox#CHECKBOX_DISABLED");

		ApplicationsMenu.gotoPortlet(
			category = "Security",
			panel = "Control Panel",
			portlet = "Password Policies");

		PasswordPolicies.disableChangeable(
			passwordPolicyName = "Default Password Policy",
			passwordPolicyPanel = "Password Changes");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.editPasswordInvalidCP(
			changeable = "false",
			newPassword = "password2",
			userScreenName = "usersn");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		UserBar.gotoDropdownItem(dropdownItem = "Account Settings");

		MyAccount.editPasswordInvalidCP(
			changeable = "false",
			newPassword = "password2");
	}

	@description = "This is a use case for LPS-92178"
	@priority = "5"
	test ViewUserAccountExpired {
		property custom.properties = "jsonws.web.service.paths.excludes=";
		property environment.acceptance = "false";

		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		JSONUser.setFirstPassword(
			agreeToTermsAndAnswerReminderQuery = "true",
			requireReset = "false",
			userEmailAddress = "userea@liferay.com");

		ApplicationsMenu.gotoPortlet(
			category = "Security",
			panel = "Control Panel",
			portlet = "Password Policies");

		PasswordPolicies.addCP(passwordPolicyName = "Password Policy Name");

		ApplicationsMenu.gotoPortlet(
			category = "Security",
			panel = "Control Panel",
			portlet = "Password Policies");

		PasswordPolicies.enableChangeable(
			passwordPolicyName = "Password Policy Name",
			passwordPolicyPanel = "Password Changes");

		ApplicationsMenu.gotoPortlet(
			category = "Security",
			panel = "Control Panel",
			portlet = "Password Policies");

		PasswordPolicies.togglePasswordExpiration(
			enablePasswordExpiration = "true",
			graceLimit = "1",
			minimumAge = "2 Weeks",
			passwordPolicyName = "Password Policy Name",
			warningTime = "1 Week");

		ApplicationsMenu.gotoPortlet(
			category = "Security",
			panel = "Control Panel",
			portlet = "Password Policies");

		PasswordPolicies.assignMembers(
			assigneeName = "userfn userln",
			assigneeType = "Users",
			passwordPolicyName = "Password Policy Name");

		var script = '''
			import com.liferay.portal.kernel.model.User;
			import com.liferay.portal.kernel.service.ServiceContext;
			import com.liferay.portal.kernel.service.ServiceContextFactory;
			import com.liferay.portal.kernel.service.UserLocalServiceUtil;
			import java.util.Date;

			ServiceContext serviceContext = ServiceContextFactory.getInstance(
				actionRequest);

			long creatorUserId = serviceContext.getUserId();
			User creatorUser = UserLocalServiceUtil.getUser(creatorUserId);
			long companyId = creatorUser.getCompanyId();
			String screenName = "usersn";
			String password = "test";

			use(groovy.time.TimeCategory) {
				def passwordModifiedDate = new Date() - 1.months

				long userId = UserLocalServiceUtil.getUserIdByScreenName(companyId, screenName);
				UserLocalServiceUtil.updatePasswordManually(
					userId, password, false, false,
					passwordModifiedDate);
				println passwordModifiedDate;
			}
		''';

		ServerAdministration.openServerAdmin();

		ServerAdministration.executeScript(
			language = "Groovy",
			script = "${script}");

		User.logoutPG();

		User.firstLoginUI(userEmailAddress = "userea@liferay.com");

		Alert.viewErrorMessage(errorMessage = "Your password is expired. You have 0 grace login(s) remaining.");
	}

	@priority = "5"
	test ViewUserAccountLockout {
		property environment.acceptance = "false";
		property portal.acceptance = "true";

		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.editPasswordCP(
			userEmailAddress = "userea@liferay.com",
			userScreenName = "usersn");

		ApplicationsMenu.gotoPortlet(
			category = "Security",
			panel = "Control Panel",
			portlet = "Password Policies");

		PasswordPoliciesNavigator.gotoPolicy(passwordPolicyName = "Default Password Policy");

		PasswordPolicies.enableDefaultPasswordPolicyLockout();

		ApplicationsMenu.gotoPortlet(
			category = "Security",
			panel = "Control Panel",
			portlet = "Password Policies");

		PasswordPoliciesNavigator.gotoPolicy(passwordPolicyName = "Default Password Policy");

		PasswordPolicies.viewPasswordPolicyLockout(passwordPolicyName = "Default Password Policy");

		User.logoutPG();

		User.loginUserInvalidPG(
			loginAttempts = "3",
			maximumFailure = "3",
			password = "incorrectpassword",
			userEmailAddress = "userea@liferay.com");

		User.loginUserPG();

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.unlockUserAccount(userScreenName = "usersn");

		User.logoutPG();

		User.loginUserPG(userEmailAddress = "userea@liferay.com");

		User.logoutPG();

		User.loginUserPG();
	}

	@priority = "4"
	test ViewUserAccountLockoutviaOrganizationPasswordPolicy {
		property custom.properties = "jsonws.web.service.paths.excludes=";

		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		JSONUser.setFirstPassword(
			agreeToTermsAndAnswerReminderQuery = "true",
			requireReset = "false",
			userEmailAddress = "userea@liferay.com");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		Organization.addCP(
			orgName = "Organization Name",
			orgType = "Organization");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		UsersAndOrganizationsNavigator.gotoOrganizations();

		Organization.assignUser(
			orgName = "Organization Name",
			userFirstName = "userfn",
			userLastName = "userln",
			userName = "userfn userln",
			userScreenName = "usersn");

		ApplicationsMenu.gotoPortlet(
			category = "Security",
			panel = "Control Panel",
			portlet = "Password Policies");

		PasswordPolicies.addCP(
			enableLockout = "true",
			maximumFailure = "3",
			passwordPolicyName = "Password Policy Name");

		ApplicationsMenu.gotoPortlet(
			category = "Security",
			panel = "Control Panel",
			portlet = "Password Policies");

		PasswordPolicies.assignMembers(
			assigneeName = "Organization Name",
			assigneeType = "Organizations",
			passwordPolicyName = "Password Policy Name");

		ApplicationsMenu.gotoPortlet(
			category = "Security",
			panel = "Control Panel",
			portlet = "Password Policies");

		PasswordPoliciesNavigator.gotoPolicy(passwordPolicyName = "Password Policy Name");

		PasswordPolicies.viewPasswordPolicyLockout(
			maximumFailure = "3",
			passwordPolicyName = "Password Policy Name");

		User.logoutPG();

		User.loginUserInvalidPG(
			loginAttempts = "3",
			maximumFailure = "3",
			password = "incorrectpassword",
			userEmailAddress = "userea@liferay.com");

		User.loginUserPG();

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.unlockUserAccount(userScreenName = "usersn");

		User.logoutPG();

		User.loginUserPG(userEmailAddress = "userea@liferay.com");

		User.logoutPG();

		User.loginUserPG();
	}

}