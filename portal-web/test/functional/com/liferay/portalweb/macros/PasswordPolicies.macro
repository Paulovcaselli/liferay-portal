definition {

	macro addCP {
		LexiconEntry.gotoAdd();

		PortletEntry.inputName(name = "${passwordPolicyName}");

		if ("${enableLockout}" == "true") {
			var key_panel = "Lockout";

			if (IsElementPresent(locator1 = "Panel#PANEL_COLLAPSED")) {
				Panel.expandPanel(panel = "Lockout");
			}

			Check.checkToggleSwitch(
				key_toggleSwitchLabel = "Enable Lockout",
				locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");

			if (isSet(maximumFailure)) {
				Type(
					key_text = "Maximum Failure",
					locator1 = "TextInput#ANY",
					value1 = "${maximumFailure}");

				AssertTextEquals(
					key_text = "Maximum Failure",
					locator1 = "TextInput#ANY",
					value1 = "${maximumFailure}");
			}
		}

		PortletEntry.save();
	}

	macro assignMembers {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Assign Members",
			rowEntry = "${passwordPolicyName}");

		Navigator.gotoNavItem(navItem = "${assigneeType}");

		LexiconEntry.gotoAdd();

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		var key_rowEntry = "${assigneeName}";

		LexiconEntry.selectEntry(key_rowEntry = "${assigneeName}");

		SelectFrameTop();

		Button.clickAdd();

		Alert.viewSuccessMessage();
	}

	macro disableChangeable {
		PasswordPoliciesNavigator.gotoPolicyPanel(
			passwordPolicyName = "${passwordPolicyName}",
			passwordPolicyPanel = "${passwordPolicyPanel}");

		Uncheck.uncheckToggleSwitch(
			key_toggleSwitchLabel = "Changeable",
			locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");

		PortletEntry.save();
	}

	macro disableHistory {
		PasswordPoliciesNavigator.gotoPolicyPanel(
			passwordPolicyName = "${passwordPolicyName}",
			passwordPolicyPanel = "${passwordPolicyPanel}");

		Uncheck.uncheckToggleSwitch(
			key_toggleSwitchLabel = "Enable History",
			locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");

		PortletEntry.save();
	}

	macro editSyntaxChecking {
		PasswordPoliciesNavigator.gotoPolicy(passwordPolicyName = "${passwordPolicyName}");

		Panel.expandPanel(panel = "Password Syntax Checking");

		if (isSet(minimumAlphaNumeric)) {
			Type(
				key_text = "Minimum Alpha Numeric",
				locator1 = "TextInput#ANY",
				value1 = "${minimumAlphaNumeric}");
		}

		if (isSet(minimumLength)) {
			Type(
				key_text = "Minimum Length",
				locator1 = "TextInput#ANY",
				value1 = "${minimumLength}");
		}

		if (isSet(minimumLowerCase)) {
			Type(
				key_text = "Minimum Lower Case",
				locator1 = "TextInput#ANY",
				value1 = "${minimumLowerCase}");
		}

		if (isSet(minimumNumbers)) {
			Type(
				key_text = "Minimum Numbers",
				locator1 = "TextInput#ANY",
				value1 = "${minimumNumbers}");
		}

		if (isSet(minimumSymbols)) {
			Type(
				key_text = "Minimum Symbols",
				locator1 = "TextInput#ANY",
				value1 = "${minimumSymbols}");
		}

		if (isSet(minimumUpperCase)) {
			Type(
				key_text = "Minimum Upper Case",
				locator1 = "TextInput#ANY",
				value1 = "${minimumUpperCase}");
		}

		if (isSet(regularExpression)) {
			Type(
				key_text = "regex",
				locator1 = "TextArea#ANY",
				value1 = "${regularExpression}");
		}

		PortletEntry.save();
	}

	macro enableChangeable {
		PasswordPoliciesNavigator.gotoPolicyPanel(
			passwordPolicyName = "${passwordPolicyName}",
			passwordPolicyPanel = "${passwordPolicyPanel}");

		Check.checkToggleSwitch(
			key_toggleSwitchLabel = "Changeable",
			locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");

		if ("${enableChangeRequired}" == "true") {
			Check.checkToggleSwitch(
				key_toggleSwitchLabel = "Change Required",
				locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");
		}
		else {
			Uncheck.uncheckToggleSwitch(
				key_toggleSwitchLabel = "Change Required",
				locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");
		}

		if (isSet(minimumAge)) {
			Select(
				key_selectFieldLabel = "Minimum Age",
				locator1 = "Select#ANY",
				value1 = "${minimumAge}");
		}

		if (isSet(resetTicketMaxAge)) {
			Select(
				key_selectFieldLabel = "Reset Ticket Max Age",
				locator1 = "Select#ANY",
				value1 = "${resetTicketMaxAge}");
		}

		PortletEntry.save();
	}

	macro enableDefaultPasswordPolicyLockout {
		Panel.expandPanel(panel = "Lockout");

		Check.checkToggleSwitch(
			key_toggleSwitchLabel = "Enable Lockout",
			locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");

		AssertTextEquals(
			key_text = "Maximum Failure",
			locator1 = "TextInput#ANY",
			value1 = "3");

		PortletEntry.save();
	}

	macro enableHistory {
		PasswordPoliciesNavigator.gotoPolicyPanel(
			passwordPolicyName = "${passwordPolicyName}",
			passwordPolicyPanel = "${passwordPolicyPanel}");

		Check.checkToggleSwitch(
			key_toggleSwitchLabel = "Enable History",
			locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");

		if (isSet(historyCount)) {
			Select(
				key_selectFieldLabel = "History Count",
				locator1 = "Select#ANY",
				value1 = "${historyCount}");
		}

		PortletEntry.save();
	}

	macro enableSyntaxChecking {
		PasswordPoliciesNavigator.gotoPolicy(passwordPolicyName = "${passwordPolicyName}");

		Panel.expandPanel(panel = "Password Syntax Checking");

		Check.checkToggleSwitch(
			key_toggleSwitchLabel = "Enable Syntax Checking",
			locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");

		if ("${allowDictionaryWords}" == "false") {
			Uncheck.uncheckToggleSwitch(
				key_toggleSwitchLabel = "Allow Dictionary Words",
				locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");
		}

		PortletEntry.save();
	}

	macro setLockoutDuration {
		Panel.expandPanel(panel = "Lockout");

		SelectField.select(
			selectFieldLabel = "Lockout Duration",
			selectFieldValue = "${duration}");

		PortletEntry.save();
	}

	macro tearDownCP {
		var passwordPolicyName = "Default Password Policy";

		ApplicationsMenu.gotoPortlet(
			category = "Security",
			panel = "Control Panel",
			portlet = "Password Policies");

		var key_passwordPolicyName = "${passwordPolicyName}";

		LexiconEntry.openEntryMenu(rowEntry = "${passwordPolicyName}");

		MenuItem.click(menuItem = "Edit");

		PasswordPolicies.enableChangeable(
			minimumAge = "None",
			passwordPolicyName = "Default Password Policy",
			passwordPolicyPanel = "Password Changes");

		Uncheck.uncheckToggleSwitch(
			key_toggleSwitchLabel = "Enable Syntax Checking",
			locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");

		Uncheck.uncheckToggleSwitch(
			key_toggleSwitchLabel = "Enable History",
			locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");

		Uncheck.uncheckToggleSwitch(
			key_toggleSwitchLabel = "Enable Expiration",
			locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");

		Uncheck.uncheckToggleSwitch(
			key_toggleSwitchLabel = "Enable Lockout",
			locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");

		PortletEntry.save();
	}

	macro togglePasswordExpiration {
		PasswordPoliciesNavigator.gotoPolicyPanel(
			passwordPolicyName = "${passwordPolicyName}",
			passwordPolicyPanel = "Password Expiration");

		if ("${enablePasswordExpiration}" == "true") {
			Check.checkToggleSwitch(
				key_toggleSwitchLabel = "Enable Expiration",
				locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");
		}
		else {
			Uncheck.uncheckToggleSwitch(
				key_toggleSwitchLabel = "Enable Expiration",
				locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");
		}

		if (isSet(minimumAge)) {
			Select(
				key_selectFieldLabel = "Maximum Age",
				locator1 = "Select#ANY",
				value1 = "${minimumAge}");
		}

		if (isSet(warningTime)) {
			Select(
				key_selectFieldLabel = "Warning Time",
				locator1 = "Select#ANY",
				value1 = "${warningTime}");
		}

		if (isSet(graceLimit)) {
			Type(
				key_text = "Grace Limit",
				locator1 = "TextInput#ANY",
				value1 = "${graceLimit}");
		}

		PortletEntry.save();
	}

	macro viewPasswordPolicyLockout {
		var key_panel = "Lockout";

		if (IsElementPresent(locator1 = "Panel#PANEL_COLLAPSED")) {
			Panel.expandPanel(panel = "Lockout");
		}

		if ("${lockoutToggle}" == "disabled") {
			AssertNotChecked.assertNotCheckedNotVisible(
				key_toggleSwitchLabel = "Enable Lockout",
				locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");
		}
		else {
			AssertChecked.assertCheckedNotVisible(
				key_toggleSwitchLabel = "Enable Lockout",
				locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");

			if (isSet(maximumFailure)) {
				AssertTextEquals(
					key_text = "Maximum Failure",
					locator1 = "TextInput#ANY",
					value1 = "${maximumFailure}");
			}
			else {
				AssertTextEquals(
					key_text = "Maximum Failure",
					locator1 = "TextInput#ANY",
					value1 = "3");
			}
		}
	}

}